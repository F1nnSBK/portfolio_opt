plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    // id 'org.graalvm.buildtools.native' version '0.11.3'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

group = 'de.hertsch'
version = '0.0.1-SNAPSHOT'
description = 'High-Performance Markowitz Optimizer'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    set('wiremockVersion', '3.9.2')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
}

tasks.withType(org.springframework.boot.gradle.tasks.aot.ProcessAot) {
    enabled = false
}
tasks.withType(org.springframework.boot.gradle.tasks.aot.ProcessTestAot) {
    enabled = false
}

tasks.named('test') {
    useJUnitPlatform()

    jvmArgs += [
        '--add-modules', 'jdk.incubator.vector',
        '--enable-native-access=ALL-UNNAMED',
        '-XX:+UnlockDiagnosticVMOptions',
        '-XX:+UnlockExperimentalVMOptions'
    ]

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

tasks.named('bootRun') {
    jvmArgs += [
        '--add-modules', 'jdk.incubator.vector',
        '--enable-native-access=ALL-UNNAMED'
    ]
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}